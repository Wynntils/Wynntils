plugins {
    id "com.gradleup.shadow" version "${shadow_version}"
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    shadowCommon
    implementation.extendsFrom shadowImplementation
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    include(modImplementation(fabricApi.module("fabric-resource-loader-v0", fabric_version)))

    modCompileOnly("com.terraformersmc:modmenu:${modmenu_version}")

    testImplementation(common(project(path: ":common", configuration: "namedElements"))) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    shadowImplementation("net.neoforged:bus:${neoforge_eventbus_version}") {
        exclude group: "org.ow2.asm"
        exclude group: "org.apache.logging.log4j"
        exclude group: "cpw.mods", module: "modlauncher"
    }

    // Hades Protocol
    shadowImplementation("com.github.wynntils:hades:v${hades_version}") { transitive false }
    shadowImplementation("com.github.wynntils:antiope:v${antiope_version}") { transitive false }

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixinextras_version}")))

    modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:${devauth_version}"

    testImplementation("net.fabricmc:fabric-loader-junit:${fabric_loader_version}")
    testImplementation("org.junit.jupiter:junit-jupiter:${junit_version}")
}

test {
    useJUnitPlatform()
}

processResources {
    var replaceProperties = [
        mod_version                : rootProject.version,
        minecraft_version          : fabric_minecraft_version_range,
        fabric_loader_version      : fabric_loader_version,
        mod_id                     : mod_id
    ]
    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }
}

shadow {
    addShadowVariantIntoJavaComponent = false
}

jar {
    archiveClassifier = "dev"
}

shadowJar {
    configurations = [
        project.configurations.shadowCommon,
        project.configurations.shadowImplementation
    ]

    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    archiveClassifier = "fabric+MC-${minecraft_version}"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}
